#!/usr/local/bin/awful

!fun :add-object $name
	!global &obj

	:writeln s'added new object ' $name

	:set &obj[$name] :dict
	:set &obj[$name][s'depth'] i0
	:set &obj[$name][s'parent'] s''
	:set &obj[$name][s'children'] :arr
!nuf


!fun :add-orbit $name $parent
	!global &obj

	:add-object $name
	:set &obj[$name][s'parent'] $parent

	!if :arr-contains (:dict-keys &obj) $parent
		:set &idx :arr-count &obj[$parent][s'children']
	!else
		:add-object $parent
		:set &idx i0
	!fi

	:set &obj[$parent][s'children'][$idx] $name

	:writeln s'added orbit: ' $name s' orbits ' $parent
!nuf


!fun :resolve-orbits $parent
	!global &obj

	:set &i i0
	!while :lt $i :arr-count &obj[&parent][s'children']
		:set &child &obj[&parent][s'children'][&i]

		:set &obj[&child][s'parent'] $parent
		:set &obj[&child][s'depth'] :add i1 &obj[&parent][s'depth']

		:writeln sxobject's "x $parent s'" child #' $i s' is ' $child s' (depth: ' &obj[&child][s'depth'] s')'

		:resolve-orbits $child
		:add &i i1
	!done
!nuf


!fun :count-orbits
	!global &obj

	:set &sum i0
	
	:set &keys :dict-keys &obj
	:set &i i0
	!while :lt $i :arr-count $keys
		:add &sum &obj[&keys[&i]][s'depth']
		:add &i i1
	!done

	!return &sum
!nuf


!fun :find-path_r $src $dst &visited
	!global &obj

	:set &visited[$src] i1

	# :writeln s'checking path from <' $src s'>(' &visited[$src] s') to <' $dst s'>'

	:set &parent &obj[$src][s'parent']
	!if :neq $parent s''
		!if :eq $parent $dst
			:write $dst s' <- '
			!return i1
		!fi
		
		!if :not &visited[$parent]
			:set &parent-path :find-path_r $parent $dst &visited
			!if :gt $parent-path i0
				:write $parent s' <- '
				!return :add i1 $parent-path
			!fi
		!fi
	!fi

	:set &i i0
	!while :lt $i :arr-count &obj[&src][s'children']
		:set &child &obj[&src][s'children'][&i]

		!if :not &visited[$child]
			!if :eq $child $dst
				:write $child s' <- '
				!return i1
			!fi

			:set &child-path :find-path_r $child $dst &visited
			!if :gt $child-path i0
				:write $child s' <- '
				!return :add i1 $child-path
			!fi
		!fi

		:add &i i1
	!done

	!return i-1
!nuf


!fun :find-path $src $dst
	!global &obj

	!if :eq $src $dst
		!return i0
	!fi

	:set &visited :dict

	:set &keys :dict-keys &obj
	:set &i i0
	!while :lt $i :arr-count $keys
		:set &visited[&keys[&i]] i0
		:add &i i1
	!done

	:set &path-len :find-path_r $src $dst &visited
	:writeln $src
	!return $path-len
!nuf


!fun :read-input
	!global &obj

	!while :not :stdin-eof
		:set &line s''
		:readln &line
		
		:set &parts :str-explode $line s')'
		:add-orbit $parts[i1] $parts[i0]
	!done
!nuf


:set &obj :dict
:add-object s'COM'

:read-input
:resolve-orbits s'COM'
:writeln :count-orbits

:writeln :find-path s'YOU' s'SAN'
