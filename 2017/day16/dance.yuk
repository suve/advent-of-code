#!/usr/bin/awful

:set &prognum i0

:set &arg i0
!while :lt &arg :param-num
	!if :eq s'--programs' :param-str &arg
		:add &arg i1
		:set &prognum :param-str &arg
	!fi
	:add &arg i1
!done

!if :eq &prognum i0
	:writeln s'You must specify the number of programs dancing: "--programs X"'
	:exit
!else
	:set &p i0
	:set &prog :arr
	!while :lt &p &prognum
		:set &prog[&p] :chr :add i97 &p
		:add &p i1
	!done
!fi


!fun :dance-spin $num
	:set &num :sub $prognum $num
	
	:set &copy &prog
	:set &p i0
	!while :lt &p &prognum
		:set &prog[&p] &copy[:mod (:add $p $num) &prognum]
		:add &p i1
	!done
!nuf

!fun :dance-exchange $pos-a $pos-b
	:set &temp &prog[&pos-a]
	:set &prog[&pos-a] &prog[&pos-b]
	:set &prog[&pos-b] &temp
!nuf

!fun :dance-partner $prog-a $prog-b
	:set &pos-a &pos-b i0
	!while :neq &prog[&pos-a] &prog-a
		:add &pos-a i1
	!done
	!while :neq &prog[&pos-b] &prog-b
		:add &pos-b i1
	!done
	
	:dance-exchange &pos-a &pos-b
!nuf


:set &input-line s''
:readln &input-line
:set &input :str-explode &input-line s','


:set &idx i0
:set &len :arr-count &input
!while :lt &idx &len
	:set &cmd &input[&idx][i1]
	:set &args :str-sub &input[&idx] i2
	
	:write s'> ' &input[&idx] s': ' :str-implode &prog sss
	
	!if :eq &cmd s's'
		:dance-spin &args
	!elif :eq &cmd s'x'
		:set &args-arr :str-explode &args s'/'
		:dance-exchange &args-arr[i0] &args-arr[i1]
	!elif :eq &cmd s'p'
		:set &args-arr :str-explode &args s'/'
		:dance-partner &args-arr[i0] &args-arr[i1]
	!fi
	
	:writeln s' -> ' :str-implode &prog sss
	:add &idx i1
!done
:writeln :str-implode &prog sss
